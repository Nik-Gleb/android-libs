/*
 * build.gradle
 * network
 *
 * Copyright (C) 2017, Gleb Nikitenko. All Rights Reserved.
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

apply plugin: 'com.android.library'


/*================== DECLARATIONS =====================*/

final int SDK_VER = Integer.parseInt(sdkVer)
final MODULE = "network"
final PACKAGE = glebLibs

final REPOSITORY = MODULE + "-repository"
final STORAGE = MODULE + "-storage"

/*=====================================================*/

android {
  compileSdkVersion SDK_VER
  buildToolsVersion buildToolsVer

  signingConfigs {
    releaseConfig {
      storeFile file(keyStoreFile)
      storePassword keyStorePass
      keyAlias sigKeyAlias
      keyPassword sigKeyPassword
    }
  }

  //noinspection GroovyMissingReturnStatement
  defaultConfig {
    minSdkVersion minSdk; targetSdkVersion SDK_VER
    dexOptions {
      javaMaxHeapSize jvmHeapSize
      maxProcessCount 8
      preDexLibraries true
      keepRuntimeAnnotatedClasses false
    }
    compileOptions {
      sourceCompatibility JavaVersion.VERSION_1_8
      targetCompatibility JavaVersion.VERSION_1_8
      incremental true
    }
    externalNativeBuild {
      cmake {
        arguments "-DANDROID_ARM_NEON=TRUE",
            "-DANDROID_TOOLCHAIN=clang",
            "-DANDROID_STL=c++_shared"
        cppFlags += "-std=c++1z -frtti -fexceptions" //-std=cuda*/
        //cFlags += "-mssse3"
        abiFilters= ["armeabi-v7a", "arm64-v8a", "x86", "x86_64"]
      }
    }
    consumerProguardFiles 'proguard-rules.pro'
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
  }

  buildTypes {
    debug {
      debuggable true; useProguard false
      shrinkResources false; minifyEnabled false
      manifestPlaceholders = [exported: false]
    }
    release {
      debuggable false; useProguard true
      // for alpha workaround
      shrinkResources false; minifyEnabled true
      manifestPlaceholders = [exported: true]
      proguardFile 'proguard-rules.pro'
      signingConfig signingConfigs.releaseConfig
    }
  }

  //testBuildType "release"

  flavorDimensions 'default'
  productFlavors {
    repository {dimension 'default'}
    storage {dimension 'default'}
  }

  externalNativeBuild {cmake {path 'CMakeLists.txt'}}

}

apply plugin: 'maven-publish'
publishing {
  publications {
    repository(MavenPublication) {
      groupId PACKAGE; artifactId REPOSITORY; version project.ext.mPubVer
      //artifact "$buildDir/outputs/aar/${archivesBaseName}-repository-release.aar"
      artifact "$buildDir/intermediates/bundles/repositoryRelease/classes.jar"
    }
    storage(MavenPublication) {
      groupId PACKAGE; artifactId STORAGE; version project.ext.mPubVer
      artifact "$buildDir/outputs/aar/${archivesBaseName}-storage-release.aar"
      //artifact "$buildDir/intermediates/bundles/storageRelease/classes.jar"
    }
  }
  repositories { maven { url myMavenRepoWriteUrl } }
}
publish.doLast {
  final stCachePath = PACKAGE + "/" + STORAGE + "/" + project.ext.mPubVer
  delete project.ext.mGradleCachePath + gradleCacheDescr + stCachePath
  delete project.ext.mGradleCachePath + gradleCacheFiles + stCachePath
  final repCachePath = PACKAGE + "/" + REPOSITORY + "/" + project.ext.mPubVer
  delete project.ext.mGradleCachePath + gradleCacheDescr + repCachePath
  delete project.ext.mGradleCachePath + gradleCacheFiles + repCachePath
}

dependencies {
  /* Proguard rules */
  //implementation 'com.infstory:proguard-annotations:1.0.2'
  /* Support annotations. */
  /*implementation 'com.android.support:support-annotations:' + supportVersion
    implementation 'com.android.support:support-core-utils:' + supportVersion
    implementation 'com.android.support:support-fragment:' + supportVersion*/

  /** OkHttp library */
  implementation 'com.squareup.okhttp3:okhttp:3.8.0'
  implementation project(':ok-tools')
  implementation project(':clean')

  /* JUnit */
  testImplementation 'junit:junit:' + junitVersion
  /* Android JUnit Runner */
  androidTestImplementation 'com.android.support.test:runner:' + runnerVersion

}
apply plugin: 'com.getkeepsafe.dexcount'